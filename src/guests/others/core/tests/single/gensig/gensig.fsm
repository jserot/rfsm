-- Generates an impulsion of duration 3.Th on output [s] whenever [e=1] when [h] occurs
-- See ../../../simple/single/gensig for a generalized version, with the impulsion duration specified as a model parameter

-- First version : Moore-style output

fsm model gensig1(
  in h: event,
  in e: bool,
  out s: bool)
{
  states: E0 where s=0, E1 where s=1;
  vars: k: int;
  trans:
  | E0 -> E1 on h when e=1 with k:=1
  | E1 -> E1 on h when k<3 with k:=k+1
  | E1 -> E0 on h when k=3;
  itrans:
  | -> E0;
}

-- Second version. Mealy-style output (with output [s] assigned in transitions rather than states)

fsm model gensig2 (
  in h: event,
  in e: bool,
  out s: bool)
{
  states: E0, E1;
  vars: k: int;
  trans:
  | E0 -> E1 on h when e=1 with k:=1,s:=1
  | E1 -> E1 on h when k<3 with k:=k+1
  | E1 -> E0 on h when k=3 with s:=0;
  itrans:
  | -> E0 with s:=0;
}
