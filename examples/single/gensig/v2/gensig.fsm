# This is a generalisation of ../v1/gensig.fsm
# using a parameterized model
# Generates an impulsion of duration n.Th on output [s]
# whenever [e=1] when [h] occurs
# Here the model is instanciated with n=4 as [g4]

type bit = int<0..1>

fsm model gensig<n:int> (
  in h: event,
  in  e: bit,
  out s: bit)
  {
  states: E0, E1;
  vars: k: int<0..n>;
  trans: 
    E0 -- h.e=1 | k:=1; s:=1 -> E1,
    E1 -- h.k<n | k:=k+1 -> E1,
    E1 -- h.k=n | s:=0 -> E0;
  itrans: | s:=0 -> E0;
  }

input H : event = periodic (10,0,80)
input E : bit = value_changes (0:0, 25:1, 35:0)
output S : bit 

fsm g4 = gensig<4>(H,E,S)
